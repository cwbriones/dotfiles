"vim: set syntax=vim"
"
"use hyper:
" Plugins managed by vim-plug
"======================================================================
call plug#begin('~/.vim/bundle')
let mapleader = ","

let g:python3_host_prog = expand('~/.venvs/neovim/bin/python')
let g:python_host_skip_check=1

" General Niceities
Plug 'Yggdroot/indentLine'
Plug 'godlygeek/tabular'
Plug 'scrooloose/nerdtree'
Plug 'simnalamburt/vim-mundo'
Plug 'tomtom/tcomment_vim'
Plug 'tpope/vim-fugitive'
Plug 'vim-airline/vim-airline'
Plug 'junegunn/fzf', { 'do' : { -> fzf#install() } }
Plug 'junegunn/fzf.vim'
Plug 'justinmk/vim-sneak'
Plug 'dag/vim-fish'

" Completion
Plug 'neoclide/coc.nvim', {'branch' : 'release'}

" Additional language support
Plug 'fatih/vim-go'

" Language syntax support
Plug 'leafgarland/typescript-vim'
Plug 'rust-lang/rust.vim'
Plug 'vim-python/python-syntax'
Plug 'cespare/vim-toml'
Plug 'udalov/kotlin-vim'

" Colorschemes
Plug 'joshdick/onedark.vim'
Plug 'chriskempson/base16-vim'
Plug 'morhetz/gruvbox'
Plug 'arcticicestudio/nord-vim'

call plug#end()

" Python
let python_highlight_all = 1

" vim-go provides convenient mappings to goimports, gofmt, staticcheck, etc.
" since coc is used for completion disable gopls and its vim-go mappings
let g:go_gopls_enabled = 0
let g:go_doc_keywordprg_enabled = 0
let g:go_def_mapping_enabled = 0
let g:go_imports_autosave = 0
let g:go_fmt_command = "gofmt"
au FileType go nmap <leader>i :GoImports<cr>

"======================================================================
" Completion
"======================================================================
" Most of these are taken verbatim from
" https://github.com/neoclide/coc.nvim#example-vim-configuration

set cmdheight=2
set updatetime=300
set shortmess+=c
" C-Space: summon autocompletion
inoremap <silent><expr> <C-Space> coc#refresh()
" selection completion using tab
inoremap <silent><expr><tab> pumvisible() ? "\<c-n>" : "\<tab>"
inoremap <silent><expr><s-tab> pumvisible() ? "\<c-p>" : "\<s-tab>"
" confirm completion with <cr>.
if exists('*complete_info')
  inoremap <expr> <cr> complete_info()["selected"] != "-1" ? "\<C-y>" : "\<C-g>u\<CR>"
else
  inoremap <expr> <cr> pumvisible() ? "\<C-y>" : "\<C-g>u\<CR>"
endif
nmap <leader>rn <Plug>(coc-rename)
nmap <leader>gd <Plug>(coc-definition)
nmap <leader>gy <Plug>(coc-type-definition)
nmap <leader>gi <Plug>(coc-implementation)
nmap <leader>fu <Plug>(coc-references)
nmap <leader>qa <Plug>(coc-codeaction)
nmap <leader>qf <Plug>(coc-fix-current)

nmap <leader>gp <Plug>(coc-diagnostic-prev)
nmap <leader>gg <Plug>(coc-diagnostic-next)

nnoremap <silent> K :call <SID>show_documentation()<CR>
function! s:show_documentation()
  if (index(['vim','help'], &filetype) >= 0)
    execute 'h '.expand('<cword>')
  elseif coc#float#has_float()
    call coc#float#close_all()
  else
    call CocAction('doHover')
  endif
endfunction

" Highlight the symbol and its references when holding the cursor.
autocmd CursorHold * silent call CocActionAsync('highlight')
highlight link CocHighlightText IncSearch
highlight Cursor guifg=white guibg=black

au BufRead,BufNewFile *.vue set filetype=html
au BufRead,BufNewFile *.svelte set filetype=html

"======================================================================
" Primary Settings
"======================================================================

" Temporary flag to enable mouse
" See https://github.com/neovim/neovim/issues/6082
set mouse=a

" Limit syntax highlighting
set synmaxcol=150
let g:matchparen_timeout = 2
let g:matchparen_insert_timeout = 2

set encoding=utf-8
set fileformat=unix
set hidden

" Basic Appearance
set nowrap
set ruler
set number
set bg=dark
set splitright
set splitbelow
set title
set cursorline
set laststatus=2
set foldenable
set foldlevel=99
set foldmethod=syntax

set completeopt=menuone,preview
let pumheight=15

" Global tab and indentation settings
set textwidth=120

set listchars="eol:$,tab:»"
set tabstop=4
set softtabstop=4
set shiftwidth=4
set expandtab
set autoindent
set smarttab
set smartindent

" Allow backspaces in insert mode
set backspace=indent,eol,start

" Search settings
set incsearch
set hlsearch
set smartcase
set ignorecase
" Remember lots
set history=1000
set undolevels=1000

" I have git for this.
set nobackup
set noswapfile

set wildignore+=*.swp,*.back,*.pyc,*.class,*.beam
" don't beep
set visualbell
set noerrorbells

"======================================================================
" Appearance
"======================================================================

let g:indentLine_char='┊'
let g:indentLine_enabled=0
let g:indentLine_setColors=1

let fullcolor_colorscheme="gruvbox"
if has("Mac")
    let system_theme = trim(system('defaults read -g AppleInterfaceStyle'))
    if system_theme == 'Dark'
        set bg=dark
    else
        set bg=dark
    endif
endif

if $COLORTERM == 'gnome-terminal'
  set t_Co=256
endif
if exists('+termguicolors')
  let &t_8f="\<Esc>[38;2;%lu;%lu;%lum"
  let &t_8b="\<Esc>[48;2;%lu;%lu;%lum"
  set termguicolors
endif
if has("gui_running")
    set lines=60
    set columns=100

    if has("win32")
        set guifont=Consolas:h11
    elseif has("Mac")
        set guifont=Inconsolata\ for\ Powerline:h18
        set macligatures
    else
        set guifont=Inconsolata\ for\ Powerline\ 12
    endif
    execute "colorscheme ".fullcolor_colorscheme
    set guioptions=aegimrLtT "Put gvim into fullscreen"
    map <silent> <F11> :call ToggleFullScreen()<CR>
elseif has("nvim")
    execute "colorscheme ".fullcolor_colorscheme
else
    colorscheme solarized
endif

" Visual indicator of extraneous whitespace
highlight ExtraWhitespace ctermbg=red guibg=red
match ExtraWhitespace /\s\+$/
command! Clean execute "%s/\\s\\+$//g"

function! ToggleVerticalRules()
    if &colorcolumn == 101
        set colorcolumn=0
        execute ':IndentLinesDisable'
    else
        set colorcolumn=101
        execute ':IndentLinesEnable'
    endif
endfunction

function! CocToggle()
    if !exists('b:coc_enabled')
        let b:coc_enabled = 1
    endif
    if b:coc_enabled == 1
        let b:coc_enabled = 0
        call coc#float#close_all()
        execute ':CocDisable'
    else
        let b:coc_enabled = 1
        execute ':CocEnable'
    endif
endfunction

" For easy editing of plain text
autocmd Filetype python setlocal fo+=r

" Disabled for now. Toggle relativenumber when in normal mode.
" set number relativenumber
"
" augroup numbertoggle
"   autocmd!
"   autocmd BufEnter,FocusGained,InsertLeave * set relativenumber
"   autocmd BufLeave,FocusLost,InsertEnter   * set norelativenumber
" augroup END

" ===================================================================
" Key Mappings
" ===================================================================
map  <leader>td <Plug>TaskList
nmap <leader>a: :Tabularize /:<CR>
nmap <leader>a= :Tabularize /=<CR>
vmap <leader>a: :Tabularize /:<CR>
vmap <leader>a= :Tabularize /=<CR>
vmap <leader>a; :Tabularize /::<CR>
vmap <leader>a- :Tabularize /-><CR>
nmap <silent> <leader>ev :e $MYVIMRC<CR>
nmap <silent> <leader>sv :so $MYVIMRC<CR>
nnoremap <leader>w <C-w>v<C-w>l

" Quickly move between splits.
nnoremap <C-j> <C-w><C-j>
nnoremap <C-k> <C-w><C-k>
nnoremap <C-l> <C-w><C-l>
nnoremap <C-h> <C-w><C-h>

" FZF
command! -bang -nargs=* Rg
  \ call fzf#vim#grep(
  \   <bang>0 ? 'rg --column --line-number --no-heading --color=always --smart-case '.<q-args>
  \           : 'rg --column --line-number --no-heading --color=always --smart-case '.shellescape(<q-args>), 1,
  \   fzf#vim#with_preview('right:50%:hidden', '?'),
  \   0)
nnoremap <C-p> :Files<CR>
noremap \ :Rg 
noremap \\ :Rg! 

" <F1> is mapped to vim help by the OS
function! ToggleLocationList()
  let g:wi = getloclist(2, {'winid' : 1})
  if g:wi != {}
    lclose
  else
    lopen
  endif
endfunction
noremap <F2> <ESC>:call ToggleLocationList()<CR>
noremap <F3> :NERDTreeToggle<CR>

" Clear recent search
noremap <F4> :call CocToggle()<CR>
noremap <F5> :set hlsearch!<CR>
noremap <F6> :call ToggleVerticalRules()<CR>
noremap <F7> :MundoToggle<CR>
inoremap jj <ESC>

noremap <leader>y "*y
noremap <leader>p "*p
noremap <leader>Y "+y
noremap <leader>P "+p

noremap <leader>fi :lope<CR>
noremap <leader>cc :set cursorline!<CR>
noremap <leader>x :Explore<CR>

" See https://vim.fandom.com/wiki/Moving_lines_up_or_down#Mappings_to_move_lines
" nnoremap <M-j> :m .+1<CR>==
" nnoremap <M-k> :m .-2<CR>==
" inoremap <M-j> <Esc>:m .+1<CR>==gi
" inoremap <M-k> <Esc>:m .-2<CR>==gi
" vnoremap <M-j> :m '>+1<CR>gv=gv
" vnoremap <M-k> :m '<-2<CR>gv=gv<Paste>

command! -nargs=* Plain set wrap linebreak nolist showbreak=…
vmap <D-j> gj
vmap <D-k> gk
vmap <D-4> g$
vmap <D-6> g^
nmap <D-j> gj
nmap <D-k> gk
nmap <D-4> g$
nmap <D-6> g^

" ===================================================================
" Additional Plugin Settings
" ===================================================================

let NERDTreeIgnore = ['\.pyc$']

" Airline
if !exists("g:airline_symbols")
    let g:airline_symbols = {}
    let g:airline_left_sep = ' '
    let g:airline_left_alt_sep = '|'
    let g:airline_right_sep = ' '
    let g:airline_right_alt_sep = '|'
    let g:airline_symbols.branch = ''
    let g:airline_symbols.readonly = ''
    let g:airline_symbols.paste = 'ρ'
    let g:airline_symbols.linenr = ''
    let g:airline_symbols.maxlinenr = ''
endif
let g:airline_skip_empty_sections = 1
let g:airline#extensions#whitespace#enabled = 0

" There's currently a bug in airline that doesn't play well with fugitive
" when both are enabled, and I'd much rather have diff still around.
" if has("nvim")
" let g:airline#extensions#branch#enabled = 0
" endif

" Load bindings that aren't synced to .dotfiles (e.g. work config)
let local_vimrc = expand('~/.localvimrc')

if filereadable(local_vimrc)
    exe 'source' local_vimrc
endif
